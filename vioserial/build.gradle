apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 34
    namespace "com.temon.serial"

    publishing {
        singleVariant("release") {
            withSourcesJar()
        }
    }

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 34
        versionCode 201
        versionName "2.0.1"

        consumerProguardFiles 'consumer-rules.pro'
        ndk {
            // Keep common ABIs for real devices + emulator.
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    externalNativeBuild {
        cmake {
            path 'CMakeLists.txt'
        }
    }
}

dependencies {

}

afterEvaluate {
    def isJitPack = System.getenv("JITPACK") != null || System.getenv("JITPACK_VERSION") != null
    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/Acccord/AndroidSerialPort")
                credentials {
                    username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                    password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
                }
            }
        }
        publications {
            release(MavenPublication) {
                from components.release
                if (isJitPack) {
                    groupId = "com.github.Acccord.AndroidSerialPort"
                    artifactId = "vioserial"
                    version = System.getenv("JITPACK_VERSION") ?: android.defaultConfig.versionName
                } else {
                    groupId = "com.temon"
                    artifactId = "serial"
                    version = android.defaultConfig.versionName
                }
            }
        }
    }
}
